subroutine mpi_halo_exchange_j(f)
   use mpi
   use cudafor
   use mod_dimensions, only: nx, ny, nz
   use mod_D3Q27setup, only: nl
   use m_mpi_decomp_init, only: north, south
   use m_mpi_pack_jplane, only: mpi_pack_jplane
   use m_mpi_unpack_jplane, only: mpi_unpack_jplane
   use m_mpi_halo_buffers
   implicit none
   real, device :: f(:,:,:,:)
   type(dim3) :: B, G
   integer :: ierr, req(4), stat(MPI_STATUS_SIZE,4)
   integer :: count_i, mpi_rtype

   B = dim3(16,8,4)
   G = dim3(((nx+2)+B%x-1)/B%x, ((nz+2)+B%y-1)/B%y, (nl+B%z-1)/B%z)

   ! --- pack boundary planes
   call mpi_pack_jplane<<<G,B>>>(f, 1,  snd_south)
   call mpi_pack_jplane<<<G,B>>>(f, ny, snd_north)

   ! --- MPI halo exchange
   if (kind(snd_south(1)) == kind(1.0d0)) then
      mpi_rtype = MPI_DOUBLE_PRECISION
   else
      mpi_rtype = MPI_REAL
   end if
   count_i = plane_elems

   call MPI_Irecv(rcv_south, count_i, mpi_rtype, south, 201, MPI_COMM_WORLD, req(1), ierr)
   call MPI_Isend(snd_south, count_i, mpi_rtype, south, 200, MPI_COMM_WORLD, req(2), ierr)
   call MPI_Irecv(rcv_north, count_i, mpi_rtype, north, 200, MPI_COMM_WORLD, req(3), ierr)
   call MPI_Isend(snd_north, count_i, mpi_rtype, north, 201, MPI_COMM_WORLD, req(4), ierr)
   call MPI_Waitall(4, req, stat, ierr)

   ! --- unpack into ghosts
   call mpi_unpack_jplane<<<G,B>>>(f, 0,    rcv_south)
   call mpi_unpack_jplane<<<G,B>>>(f, ny+1, rcv_north)

   call cudaDeviceSynchronize()
end subroutine

